C $Header: /home/jahn/src/cvs2git/MITgcm/20170915-2/gcmpack-all-patch/MITgcm/model/src/timestep.F,v 1.11 1998/06/09 16:48:03 cnh Exp $

#include "CPP_OPTIONS.h"

C     /==========================================================\
C     | S/R TIMESTEP                                             |
C     | o Step model fields forward in time                      |
C     \==========================================================/
      SUBROUTINE TIMESTEP( bi, bj, iMin, iMax, jMin, jMax,
     I                     K,
     I                     myThid )
      implicit none
! Common
#include "SIZE.h"
#include "DYNVARS.h"
#include "EEPARAMS.h"
#include "PARAMS.h"
#include "GRID.h"
C     == Routine Arguments ==
      INTEGER bi,bj,iMin,iMax,jMin,jMax
      INTEGER K
      INTEGER myThid
C     == Local variables ==
      INTEGER i,j
      _RL ab15,ab05

C     Adams-Bashforth timestepping weights
      ab15=1.5+abeps
      ab05=-0.5-abeps

C     Step forward zonal velocity (store in Gu)
       DO j=jMin,jMax
        DO i=iMin,iMax
         gUNm1(i,j,k,bi,bj)=uVel(i,j,k,bi,bj)
     &    +deltaTmom*(ab15*gU(i,j,k,bi,bj)+ab05*gUNm1(i,j,k,bi,bj)
#ifdef ALLOW_CD
     &    +guCD(i,j,k,bi,bj)
#endif
     &        )*_maskW(i,j,k,bi,bj)
        ENDDO
       ENDDO
C     Step forward meridional velocity (store in Gv)
       DO j=jMin,jMax
        DO i=iMin,iMax
         gVNm1(i,j,k,bi,bj)=vVel(i,j,k,bi,bj)
     &    +deltaTmom*(ab15*gV(i,j,k,bi,bj)+ab05*gVNm1(i,j,k,bi,bj)
#ifdef ALLOW_CD
     &    +gvCD(i,j,k,bi,bj)
#endif
     &        )*_maskS(i,j,k,bi,bj)
        ENDDO
       ENDDO
C     Step forward temperature
       DO j=jMin,jMax
        DO i=iMin,iMax
         theta(i,j,k,bi,bj)=theta(i,j,k,bi,bj)
     &    +deltaTtracer*(ab15*gT(i,j,k,bi,bj)+ab05*gTNm1(i,j,k,bi,bj))
         gTNm1(i,j,k,bi,bj)=gT(i,j,k,bi,bj)
        ENDDO
       ENDDO

      RETURN
      END
