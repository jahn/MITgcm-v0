C $Header: /home/jahn/src/cvs2git/MITgcm/20170915-2/gcmpack-all-patch/MITgcm/pkg/mom_common/mom_init_fixed.F,v 1.5 2009/09/17 16:57:13 dfer Exp $
C $Name:  $

#include "MOM_COMMON_OPTIONS.h"

C---+----1----+----2----+----3----+----4----+----5----+----6----+----7-|--+----|
CBOP
C     !ROUTINE: MOM_INIT_FIXED

C     !INTERFACE:
      SUBROUTINE MOM_INIT_FIXED( myThid )

C     !DESCRIPTION:
C     Initialize fixed quantities
C      for momentum (common to fluxform & vecinv) packages

C     !USES:
      IMPLICIT NONE
#include "SIZE.h"
#include "EEPARAMS.h"
#include "PARAMS.h"
#include "GRID.h"
#include "SURFACE.h"
#include "MOM_VISC.h"

C     !INPUT PARAMETERS:
C     myThid               :: thread number
      INTEGER myThid
CEOP

C     !LOCAL VARIABLES:
      INTEGER i,j,bi,bj
#ifdef NONLIN_FRSURF
C     i,j,k,bi,bj  :: loop counter
      INTEGER i,j,k,bi,bj
#endif

#ifdef NONLIN_FRSURF
C-    used for side-drag:
      DO bj=myByLo(myThid), myByHi(myThid)
       DO bi=myBxLo(myThid), myBxHi(myThid)
         DO k=1,Nr
          DO j=1-Oly,sNy+Oly
           DO i=1-Olx,sNx+Olx
            h0FacC(i,j,k,bi,bj) = _hFacC(i,j,k,bi,bj)
            h0FacW(i,j,k,bi,bj) = _hFacW(i,j,k,bi,bj)
            h0FacS(i,j,k,bi,bj) = _hFacS(i,j,k,bi,bj)
           ENDDO
          ENDDO
         ENDDO
       ENDDO
      ENDDO
#endif /* NONLIN_FRSURF */

       IF (deltaTmom.NE.0.) THEN
        recip_dt=1. _d 0/deltaTmom
       ELSE
        recip_dt=0. _d 0
       ENDIF

       DO bj=myByLo(myThid), myByHi(myThid)
        DO bi=myBxLo(myThid), myBxHi(myThid)
         DO j=1-Oly,sNy+Oly
          DO i=1-Olx,sNx+Olx
           IF (useAreaViscLength) THEN
            L2_D(i,j,bi,bj)=rA(i,j,bi,bj)
           ELSE
            L2_D(i,j,bi,bj)=2. _d 0
     &           /((recip_DXF(i,j,bi,bj)**2+recip_DYF(i,j,bi,bj)**2))
           ENDIF
           L3_D(i,j,bi,bj)=L2_D(i,j,bi,bj)**1.5
           L4rdt_D(i,j,bi,bj)=0.03125 _d 0*recip_dt*L2_D(i,j,bi,bj)**2
          ENDDO
         ENDDO
        ENDDO
       ENDDO

       DO bj=myByLo(myThid), myByHi(myThid)
        DO bi=myBxLo(myThid), myBxHi(myThid)
         DO j=1-Oly,sNy+Oly
          DO i=1-Olx,sNx+Olx
           IF (useAreaViscLength) THEN
            L2_Z(i,j,bi,bj)=rAz(i,j,bi,bj)
           ELSE
            L2_Z(i,j,bi,bj)=2. _d 0
     &           /((recip_DXV(I,J,bi,bj)**2+recip_DYU(I,J,bi,bj)**2))
           ENDIF
           L3_Z(i,j,bi,bj)=L2_Z(i,j,bi,bj)**1.5
           L4rdt_Z(i,j,bi,bj)=0.03125 _d 0*recip_dt*L2_Z(i,j,bi,bj)**2
          ENDDO
         ENDDO
        ENDDO
       ENDDO

#ifdef ALLOW_DIAGNOSTICS
      IF ( useDiagnostics ) THEN
        CALL MOM_DIAGNOSTICS_INIT( myThid )
      ENDIF
#endif /* ALLOW_DIAGNOSTICS */

      RETURN
      END
