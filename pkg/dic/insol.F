C $Header: /home/jahn/src/cvs2git/MITgcm/20170915-2/gcmpack-all-patch/MITgcm/pkg/dic/insol.F,v 1.12 2008/04/09 22:13:15 jmc Exp $
C $Name:  $

#include "DIC_OPTIONS.h"

CBOP
C !ROUTINE: INSOL

C !INTERFACE: ==========================================================
      SUBROUTINE insol(Time,sfac,bi,bj,myThid)

C !DESCRIPTION:
C find light as function of date and latitude
c based on paltridge and parson


C !USES: ===============================================================
      IMPLICIT NONE
C     === Global variables ===
#include "SIZE.h"
#include "EEPARAMS.h"
#include "PARAMS.h"
#include "FFIELDS.h"
#include "GRID.h"
#include "DYNVARS.h"
#include "DIC_VARS.h"

C !INPUT PARAMETERS: ===================================================
C Time                 :: current time
       _RL Time
       INTEGER bi,bj
       INTEGER myThid

C !OUPUT PARAMETERS: ===================================================
       _RL sfac(1-OLy:sNy+OLy)

#ifdef DIC_BIOTIC

C !LOCAL VARIABLES: ====================================================
       _RL  solar, albedo
       _RL  dayfrac, yday, delta
       _RL  lat, sun1, dayhrs
       _RL  cosz, frac, fluxi
       integer j
CEOP

      solar = 1360. _d 0   !solar constant
      albedo= 0.6 _d 0     !planetary albedo

#ifndef READ_PAR
C     latitute in radians, backed out from coriolis parameter
C     (makes latitude independent of grid)
      IF ( usingCurvilinearGrid ) THEN
       STOP 'S/R INSOL: use #define READ_PAR with usingCurvilinearGrid'
      ENDIF
#endif /* READ_PAR */

c
c find day (****NOTE for year starting in winter*****)
        dayfrac=mod(Time,360. _d 0*86400. _d 0)
     &                    /(360. _d 0*86400. _d 0)  !fraction of year
        yday = 2. _d 0*PI*dayfrac                    !convert to radians
        delta = (0.006918 _d 0 
     &         -(0.399912 _d 0*cos(yday))            !cosine zenith angle
     &         +(0.070257 _d 0*sin(yday))            !(paltridge+platt)
     &         -(0.006758 _d 0*cos(2. _d 0*yday))
     &         +(0.000907 _d 0*sin(2. _d 0*yday))
     &         -(0.002697 _d 0*cos(3. _d 0*yday))
     &         +(0.001480 _d 0*sin(3. _d 0*yday)) )
       DO j=1-OLy,sNy+OLy
c latitude in radians
          lat=YC(1,j,1,bj)*deg2rad
#ifndef READ_PAR
C     latitute in radians, backed out from coriolis parameter
C     (makes latitude independent of grid)
          IF ( .NOT. usingSphericalPolarGrid )
     &         lat = asin( fCori(1,j,1,bj)/(2. _d 0*omega) )*deg2rad
#endif /* READ_PAR */
          sun1 = -sin(delta)/cos(delta) * sin(lat)/cos(lat)
          IF (sun1.LE.-0.999 _d 0) sun1=-0.999 _d 0
          IF (sun1.GE. 0.999 _d 0) sun1= 0.999 _d 0
          dayhrs = abs(acos(sun1))
          cosz = ( sin(delta)*sin(lat)+              !average zenith angle
     &            (cos(delta)*cos(lat)*sin(dayhrs)/dayhrs) )
          IF (cosz.LE.5. _d -3) cosz= 5. _d -3
          frac = dayhrs/PI                           !fraction of daylight in day
c daily average photosynthetically active solar radiation just below surface
          fluxi = solar*(1. _d 0-albedo)*cosz*frac*parfrac
c
c convert to sfac
          sfac(j) = MAX(1. _d -5,fluxi)
       ENDDO !j

#endif /* DIC_BIOTIC */

      return
      end
